# 3.基于像素的操作

本章介绍了处理单个像素颜色值的不同方法，从而为图像创建有趣的效果。您将学习如何以算法和交互的方式处理单个像素。在这一章中，你将只关注改变像素的颜色值，而不是它们在图像中的位置和总数。在学习图像处理的技术细节之前，本章还将介绍艺术和设计中常用的基本图形属性。本章将涵盖以下主题:

*   视觉属性
*   像素颜色处理
*   随机性
*   用现有图像绘图
*   混合多个图像

## 视觉属性

在视觉艺术和设计专业，你学习如何创造视觉材料，并把它们组合起来。对于任何视觉材料，您通常可以用以下内容来描述其属性:

*   位置
*   大小
*   形状
*   方向
*   颜色
*   价值

在经典著作《图形符号学》中，Jacque Bertin 使用术语视网膜变量来描述视觉元素的相似属性。让我们浏览一下这些属性，看看它们中是否有任何一个与像素颜色处理的讨论有关。

### 位置

图像中的每个像素都有一个位置。如图 [3-1](#Fig1) 所示，测量的原点在左上角，而不是你可能在学校学过的左下角。水平位置是 x 轴，其值随着向右侧移动而增加。垂直位置是 y 轴，其值随着向底部移动而增加。

![A436326_1_En_3_Fig1_HTML.jpg](A436326_1_En_3_Fig1_HTML.jpg)

图 3-1。

Pixel position in an image

您可以根据像素在图像中的位置来更改像素颜色信息，以实现渐变效果。图 [3-2](#Fig2) 显示了一个典型的例子。

![A436326_1_En_3_Fig2_HTML.jpg](A436326_1_En_3_Fig2_HTML.jpg)

图 3-2。

Gradation effect

### 大小

像素没有任何大小信息。准确地说，每个像素的大小为 1 乘 1。您可以想象通过将像素周围的相邻像素更改为相同的颜色来增加像素的大小。这就是你大概熟悉的马赛克效果，如图 [3-3](#Fig3) 。

![A436326_1_En_3_Fig3_HTML.jpg](A436326_1_En_3_Fig3_HTML.jpg)

图 3-3。

Mosaic effect

### 形状

很难描述像素的形状。事实上，由于它的大小只有一个像素，所以描述像素的形状是没有意义的。从概念上讲，在矩形网格表示中，您可以将像素视为一个微小的正方形或圆形。

### 方向

如果一个像素没有确定的形状，你就不能描述它的方向(即，在二维平面上的旋转量)。但是，您可以从整体上描述数码图像的方向/旋转。在这种情况下，您正在转换图像中像素的位置，这将是下一章的主题。

### 颜色

像素的颜色信息是本章的主要内容。你将会看到如何用不同的方式改变颜色。您可以使用颜色来传达图像中的信息。如果两幅图像有两种不同的纯色，你很容易断定它们是不同的。如图 [3-4](#Fig4) 所示，如果你随意选择颜色，你无法立即分辨哪个“高”哪个“低”。

![A436326_1_En_3_Fig4_HTML.jpg](A436326_1_En_3_Fig4_HTML.jpg)

图 3-4。

Color difference

### 价值

该值有时被称为颜色的强度或亮度。如果你只是使用灰度图像，这更有意义，如图 [3-5](#Fig5) 所示。与任意使用颜色不同，它创建了一个比较来提示订单信息。例如，使用深灰色和浅灰色可能暗示重量比较，以表明某人较重或较轻。在处理中，除了使用 RGB 作为默认的颜色表示外，还可以使用 HSB(色调、饱和度、亮度)。在这种情况下，如果保持色调和饱和度不变，只改变亮度，就可以创建一个表示顺序的比较。

![A436326_1_En_3_Fig5_HTML.jpg](A436326_1_En_3_Fig5_HTML.jpg)

图 3-5。

Grayscale image with value comparison

## 像素颜色处理

在前一章中，您在处理中使用了`PImage`的`get()`和`set()`方法来获取和更新像素颜色信息。`PImage`对象有一个内部数组来存储每个像素的颜色信息。在这一章中，我将介绍一种更新`PImage`对象的内部数组`pixels[]`的直接方法。在第一个练习`Chapter03_01`中，草图通过使用`pixels[]`数组将所有像素改变为一种颜色来创建一个纯色图像。

```py
PImage img;

void setup() {
  size(750, 750);
  background(0);
  img = createImage(width, height, ARGB);
  noLoop();
}

void draw() {
  img.loadPixels();
  color orange = color(255, 160, 0);
  for (int i=0; i<img.pixels.length; i++) {
    img.pixels[i] = orange;
  }
  img.updatePixels();
  image(img, 0, 0);
}

```

注意，代码使用了一个名为`img`的`PImage`对象实例。它是使用该设置中的`createImage()`功能创建的。您还可以使用`noLoop()`功能运行一次`draw()`功能，而不会循环。在`draw()`函数中，使用`loadPixels()`方法将图像数据加载到`img`的`pixels`数组中，在`for`循环中更新`pixels`数组元素后，使用`updatePixels()`方法改变颜色。在`for`循环中，使用索引`i`从 0 开始执行重复，直到到达`pixels`数组的长度。每个像素的颜色都与变量 orange 中定义的颜色相同。`pixels[]`数组是一个整数数组，大小等于`PImage`的`width × height`(即图像的像素数)。每个像素是一个 32 位整数，以 ARGB 格式存储四个颜色通道。不用直接为一种颜色写一个整数，你可以使用`color()`函数用四个数字指定一种颜色为`color(red, green, blue, alpha)`。默认情况下，`red`、`green`、`blue`和`alpha`值都是 0 到 255 范围内的数字。

### 颜色随像素位置变化

在下一个练习`Chapter03_02`中，您将考虑像素位置来改变其颜色。它创建的结果图像将是您在“视觉属性”一节中学到的渐变效果。

```py
PImage img;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  noLoop();
}

void draw() {
  background(0);
  img.loadPixels();
  float xStep = 256.0/img.width;
  float yStep = 256.0/img.height;
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      img.pixels[rows+x] = color(x*xStep, 0, y*yStep);
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

```

图 [3-6](#Fig6) 显示运行加工草图的结果。

![A436326_1_En_3_Fig6_HTML.jpg](A436326_1_En_3_Fig6_HTML.jpg)

图 3-6。

Gradation image in two colors, red and blue

在上一个练习中，您根据像素位置的线性变化更改了颜色 RGB 分量。然而，你可以尝试另一种非线性的方法来观察差异。在下面的练习`Chapter03_03`中，您可以看到这种方法的演示:

```py
PImage img;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  noLoop();
}

void draw() {
  background(0);
  img.loadPixels();
  float colStep = 256.0/colFunc(img.height);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    color col = color(colFunc(y)*colStep);
    for (int x=0; x<img.width; x++) {
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

float colFunc(float v) {
  return v;
}

```

在第一个版本中，您使用线性颜色渐变，它实际上是 y 轴上的灰度渐变。为了灵活起见，您使用一个名为`colFunc()`的独立函数来计算像素的颜色变化和`y`位置之间的关系。在第一次运行时，您只需返回`y`位置值作为函数的输出。在`draw()`函数中，通过除以 256 来定义变量`colStep`，256 是灰度的最大值`colFunc(img.height)`，最大值来自`colFunc()`。在 y 轴的`for`循环的每一步中，颜色变量`col`通过将`colFunc(y)`乘以`colStep`值来计算。在这种情况下，当`y`位置为 0 时`col`的最小值为 0，当`y`位置为`img.height - 1`时`col`的最大值为 255。图 [3-7](#Fig7) 显示运行加工草图的结果。

![A436326_1_En_3_Fig7_HTML.jpg](A436326_1_En_3_Fig7_HTML.jpg)

图 3-7。

Grayscale gradation with linear function

在第二个版本中，您可以通过返回`v`的平方来修改`colFunc()`函数，如下所示:

```py
float colFunc(float v) {
  return v*v;
}

```

图 [3-8](#Fig8) 显示了该版本根据`y`位置灰度非线性变化的结果。

![A436326_1_En_3_Fig8_HTML.jpg](A436326_1_En_3_Fig8_HTML.jpg)

图 3-8。

Grayscale gradation with nonlinear change, y-square

在本练习的最后一个版本中，您用一个更一般的数学函数代替了`colFunc()`函数，这个函数叫做非整数值的`pow()`。你可以尝试，例如，把参数`v`的 1.5 次方。新的`colFunc()`定义如下:

```py
float colFunc(float v) {
  return (float) Math.pow(v, 1.5);
}

```

图 [3-9](#Fig9) 包含在这里，以便您可以与最后两个进行比较。

![A436326_1_En_3_Fig9_HTML.jpg](A436326_1_En_3_Fig9_HTML.jpg)

图 3-9。

Grayscale gradation with nonlinear change, y to the power of 1.5

### 颜色随像素距离变化

除了根据像素的位置更改颜色值，您还可以根据像素与屏幕上另一个位置的距离更改颜色值。在以下练习中，您将尝试不同的距离函数和位置，并查看结果。首先，试着用这个练习来比较一个像素到图像中心的距离，`Chapter03_04`:

```py
PImage img;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  noLoop();
}

void draw() {
  background(0);
  img.loadPixels();
  float colStep = 256.0/max(img.width/2, img.height/2);
  PVector ctr = new PVector(img.width/2, img.height/2);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      float d = distance(ctr, new PVector(x, y));
      color col = color(d*colStep, 0, 255-d*colStep);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

float distance(PVector p1, PVector p2) {
  float d = abs(p1.x-p2.x) + abs(p1.y-p2.y);
  return d;
}

```

请注意在名为`distance()`的程序中使用了自定义距离函数。它有两个类型为`PVector` ( [`https://processing.org/reference/PVector.html`](https://processing.org/reference/PVector.html) )的参数，这是一个在处理中很有用的类，可以简化矢量计算的使用。一个`PVector`有三个属性:`x`、`y`和`z`。这些对应于三维空间中的位置。在本练习中，您仅使用 2D 图形中的`x`和`y`。该版本的距离函数使用两点的`x`和`y`位置之间差值的绝对值之和。在`draw()`函数中，您计算每个像素到图像中心的距离，并使用它来计算红色和蓝色分量。图 [3-10](#Fig10) 显示运行加工草图的结果。

![A436326_1_En_3_Fig10_HTML.jpg](A436326_1_En_3_Fig10_HTML.jpg)

图 3-10。

Color change with distance from center

您可以修改`distance()`函数，使用更常见的欧几里德距离来测试结果。以下是`distance()`函数的新定义:

```py
float distance(PVector p1, PVector p2) {
  float d = p1.dist(p2);
  return d;
}

```

它采用`PVector`内置的`dist()`方法来计算二维空间中两点之间的距离。最终的图像(如图 [3-11](#Fig11) 所示)将看起来像一个圆形而不是菱形。

![A436326_1_En_3_Fig11_HTML.jpg](A436326_1_En_3_Fig11_HTML.jpg)

图 3-11。

Color change with distance from center

在下一个练习`Chapter03_05`中，您将增强`draw()`函数中的计算，这样您就可以在处理中利用一些好的特性来简化代码:

```py
PImage img;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  noLoop();
}

void draw() {
  background(0);
  img.loadPixels();
  float distMax = max(img.width/2, img.height/2);
  PVector ctr = new PVector(img.width/2, img.height/2);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      float d = distance(ctr, new PVector(x, y));
      float c = map(d, 0, distMax, 0, 255);
      color col = color(c, 0, 255-c);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

float distance(PVector p1, PVector p2) {
  float d = p1.dist(p2);
  return d;
}

```

使用的新功能是`map()`功能。它接受示例中的变量`d`，并将其从源范围 0 到`distMax`映射到目标范围 0 到 255。它简化了许多应用的线性映射计算。

该程序的一个快速变化是向变量`ctr`引入交互性。想象一下，如果它能跟随鼠标的移动；你可以通过使用`mouseX`和`mouseY`变量来生成它的交互版本。

```py
PImage img;
float distMax;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  distMax = max(img.width, img.height);
}

void draw() {
  background(0);
  PVector ctr = new PVector(mouseX, mouseY);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      float d = distance(ctr, new PVector(x, y));
      float c = map(d, 0, distMax, 0, 255);
      color col = color(c, 0, 255-c);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

float distance(PVector p1, PVector p2) {
  float d = p1.dist(p2);
  return d;
}

```

### 用三角函数改变颜色

三角函数指的是你在学校学过的正弦、余弦、正切函数。正弦和余弦函数的输出值具有周期性。Processing 内置了从 Java 中采用的`sin()`和`cos()`函数。它们有一个输入值，以弧度为单位。输入值的正常范围是在-PI 到 PI 的范围内，以完成一个周期。两个函数的输出范围都在-1 到 1 的范围内。在下一个练习`Chapter03_07`中，将输入范围(即像素和鼠标位置之间的距离)映射到-PI 和 PI 之间，同时将-1 到 1 之间的输出范围映射到 0 到 255 之间的颜色范围。

```py
PImage img;
float num;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  num = 8;
}

void draw() {
  background(0);
  PVector mouse = new PVector(mouseX, mouseY);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      PVector dist = distance(mouse, new PVector(x, y));
      float xRange = map(dist.x, -img.width, img.width, -PI*num, PI*num);
      float yRange = map(dist.y, -img.height, img.height, -PI*num, PI*num);
      float xCol = map(cos(xRange), -1, 1, 0, 255);
      float yCol = map(sin(yRange), -1, 1, 0, 255);
      color col = color(xCol, 0, yCol);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

PVector distance(PVector p1, PVector p2) {
  return PVector.sub(p1, p2);
}

```

您修改`distance()`函数来返回一个`PVector`,存储两个输入向量的相减结果。在输入范围中，还引入了一个新变量`num`，来扩展原来的范围(-PI，PI)。图像将由更多的重复组成。图 [3-12](#Fig12) 显示了试运行的结果。

![A436326_1_En_3_Fig12_HTML.jpg](A436326_1_En_3_Fig12_HTML.jpg)

图 3-12。

Color change with trigonometric functions

为了增强图像的复杂性，您可以简单地将`x`和`y for`循环变量添加到输入范围计算中，如下一个练习`Chapter03_08`所示。同时，您需要减少变量`num`的值，这样`xRange`和`yRange`的值就不会变得太大。

```py
PImage img;
float num;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  num = 0.1;
}

void draw() {
  background(0);
  PVector mouse = new PVector(mouseX, mouseY);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      PVector dist = distance(mouse, new PVector(x, y));
      float xRange = map(dist.x, -img.width, img.width, -PI*num*y, PI*num*x);
      float yRange = map(dist.y, -img.height, img.height, -PI*num*x, PI*num*y);
      float xCol = map(cos(xRange), -1, 1, 0, 255);
      float yCol = map(sin(yRange), -1, 1, 0, 255);
      color col = color(xCol, 0, yCol);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

PVector distance(PVector p1, PVector p2) {
  return PVector.sub(p1, p2);
}

```

这将产生一种更迷幻的效果，类似于 20 世纪 60 年代和 70 年代常见的光学艺术图形，如图 [3-13](#Fig13) 所示。因为在计算`xRange`和`yRange`值时包含了`x`和`y`值，所以结果不太容易预测。

![A436326_1_En_3_Fig13_HTML.jpg](A436326_1_En_3_Fig13_HTML.jpg)

图 3-13。

Another example with trigonometric function

在下一个练习`Chapter03_09`中，您将简化`distance()`函数，并在`draw()`函数中仅使用一个值`dist`来生成正弦和余弦函数的输入范围。修改不是实质性的，但视觉效果与之前的有很大不同。

```py
PImage img;
float num;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  num = 2;
}

void draw() {
  background(0);
  PVector mouse = new PVector(mouseX, mouseY);
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      float dist = distance(mouse, new PVector(x, y));
      float range = map(dist, -img.width, img.width, -PI*num*y, PI*num*x);
      float xCol = map(sin(range), -1, 1, 0, 255);
      float yCol = map(cos(range), -1, 1, 0, 255);
      color col = color(0, 255-xCol, yCol);
      img.pixels[rows+x] = col;
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

float distance(PVector p1, PVector p2) {
  return p1.dist(p2);
}

```

`xCol`和`yCol`颜色变量共享相同的输入范围值，但使用不同的三角函数。视觉结果可能类似于一个圆，因为您知道圆可以表示如下:

*   `x = radius * cos(angle)`
*   `y = radius * sin(angle)`

在本练习中，图像要复杂得多，因为这里表示为`angle`的输入范围不仅仅是从-PI 到 PI。图 [3-14](#Fig14) 显示运行程序后的图像。

![A436326_1_En_3_Fig14_HTML.jpg](A436326_1_En_3_Fig14_HTML.jpg)

图 3-14。

Color change with more trigonometric function

在图像处理中使用三角函数会给你带来很多乐趣。请随意探索更多的变化。在下一节，我将开始解释随机性的想法如何帮助你生成有趣的图像。

## 随机性

处理提供了一个基于`java.util.Random`类的随机数生成器。您可以使用`random()`功能创建各种类型的随机彩色图像。在下一个练习`Chapter03_10`中，你将使用随机数来填充灰度图像:

```py
PImage img;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  noLoop();
}

void draw() {
  background(0);
  for (int i=0; i<img.pixels.length; i++) {
    img.pixels[i] = color(floor(random(0, 256)));
  }
  img.updatePixels();
  image(img, 0, 0);
}

```

`draw()`函数中的`for`循环遍历`PImage`中的所有像素，并使用函数`random(0, 256)`将颜色设置为 0 到 255 之间的随机值。结果图像完全混乱，没有任何可识别的图案，如图 [3-15](#Fig15) 所示。

![A436326_1_En_3_Fig15_HTML.jpg](A436326_1_En_3_Fig15_HTML.jpg)

图 3-15。

Random grayscale image

如果您希望创建一个更具随机性的视觉愉悦的图像，可以通过在颜色信息中施加规则来降低随机性的程度。下一个练习`Chapter03_11`，将使用随机灰色调初始化图像中的第一个像素。下一个像素将增加或减少随机部分的灰度值。比较两个结果，看看第二个版本中是否有任何模式。

```py
PImage img;
float value1;
float range;

void setup() {
  size(750, 750);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  value1 = floor(random(0, 256));
  range = 50;
  noLoop();
}

void draw() {
  background(0);
  for (int i=0; i<img.pixels.length; i++) {
    float v = random(-range, range);
    value1 += v;
    value1 = constrain(value1, 0, 255);
    img.pixels[i] = color(value1);
  }
  img.updatePixels();
  image(img, 0, 0);
}

```

代码基本上使用了`random(-range, range)`语句在`draw()`函数中引入了一个受控版本的随机性。图像将由随机的灰色调像素组成，但随机性被控制在一个较小的范围内，同时依赖于前一个像素，如图 [3-16](#Fig16) 所示。

![A436326_1_En_3_Fig16_HTML.jpg](A436326_1_En_3_Fig16_HTML.jpg)

图 3-16。

Random grayscale image with patterns

由于像素颜色信息依赖于最后一个，具有一定程度的随机性，您可以很容易地识别图像的水平纹理，因为阵列中像素的排列首先按行顺序排序。

下一个练习`Chapter03_12`，在处理中使用`noise()`函数来探索随机性。这是肯·柏林开发的柏林噪声函数。该函数的输出显示了一个更自然、更平滑的数字序列。处理提供了多达三维的柏林噪声函数。在本练习中，您将使用二维版本的噪波值用灰色调填充图像。

```py
PImage img;
float xScale, yScale;

void setup() {
  size(750, 750);
  background(0);
  img = createImage(width, height, ARGB);
  img.loadPixels();
  xScale = 0.01;
  yScale = 0.01;
  noLoop();
}

void draw() {
  for (int y=0; y<img.height; y++) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x++) {
      img.pixels[rows+x] = color(floor(noise(x*xScale, y*yScale)*256));
    }
  }
  img.updatePixels();
  image(img, 0, 0);
}

```

请注意，对于像素的`x`和`y`位置，您使用`xScale`和`yScale`变量来缩小范围，以实现图像中更平滑的噪声效果，如图 [3-17](#Fig17) 所示。

![A436326_1_En_3_Fig17_HTML.jpg](A436326_1_En_3_Fig17_HTML.jpg)

图 3-17。

Grayscale color with Perlin noise

到目前为止，您已经使用算法的方式为每个像素填充颜色，创建了一个图像。您还学习了如何创建像素中带有随机颜色的图像。在下一节中，您将导入一个现有的图像，并使用前面几节中的步骤来处理像素颜色。

## 用现有图像绘图

下一个练习`Chapter03_13`，使用现有图像将彩色图像转换为灰色调。当然，您可以使用 Processing 和 OpenCV 的内置函数来进行转换。您可以将此练习作为起点，学习如何编写简单的图像处理代码。

```py
PImage img1, img2;

void setup() {
  size(1500, 750);

  background(0);
  img1 = loadImage("landscape.png");
  img1.loadPixels();
  img2 = createImage(img1.width, img1.height, ARGB);
  img2.loadPixels();
  noLoop();
}

void draw() {
  for (int i=0; i<img1.pixels.length; i++) {
    color col = img1.pixels[i];
    img2.pixels[i] = color((red(col) + green(col) + blue(col))/3);
  }
  img2.updatePixels();
  image(img1, 0, 0);
  image(img2, img1.width, 0);
}

```

在程序中，您将处理窗口定义为照片宽度的两倍，以便并排显示原始图像和修改后的图像。您使用两个`PImage`变量。第一个是`img1`，加载外部图像。第二个，`img2`，使用红色、绿色和蓝色的简单平均，将第一个的颜色像素转换成单一的灰色调。图 [3-18](#Fig18) 显示了转换过程。

![A436326_1_En_3_Fig18_HTML.jpg](A436326_1_En_3_Fig18_HTML.jpg)

图 3-18。

Color to grayscale conversion with simple averaging

还有另一种方法可以从原始 RGB 图像计算灰度图像的亮度。视觉不会检测到强度相等的 RGB。在本练习版本`Chapter03_14`中，您将使用以下公式得出亮度值:

```py
img2.pixels[i] = color(0.2*red(col) + 0.7*green(col) + 0.1*blue(col));

```

图 [3-19](#Fig19) 显示了用于比较的结果图像。

![A436326_1_En_3_Fig19_HTML.jpg](A436326_1_En_3_Fig19_HTML.jpg)

图 3-19。

Color to grayscale conversion with relative luminance

在下一个练习`Chapter03_15`中，您将编写一个反转滤镜来反转原始彩色图像的所有红色、绿色和蓝色通道。为了达到这种效果，您使用 255 并减去所有三个颜色通道值。公式如下:

```py
img2.pixels[i] = color(255-red(col), 255-green(col), 255-blue(col));

```

图 [3-20](#Fig20) 显示了结果图像。

![A436326_1_En_3_Fig20_HTML.jpg](A436326_1_En_3_Fig20_HTML.jpg)

图 3-20。

Color change with inverse effect

您也可以交换三个颜色通道，以不同的顺序混合它们，以获得 Photoshop 中可以找到的其他效果。下面是一个例子，`Chapter03_16`，它交换了三个通道的顺序，并反转了原来的红色通道:

```py
img2.pixels[i] = color(blue(col), 255-red(col), green(col));

```

图 [3-21](#Fig21) 显示了使用相同图像的输出。

![A436326_1_En_3_Fig21_HTML.jpg](A436326_1_En_3_Fig21_HTML.jpg)

图 3-21。

Color change by swapping different color channels

处理有一个`filter()`功能`(` [`https://processing.org/reference/filter_.html`](https://processing.org/reference/filter_.html) ，它提供了许多图像处理预设，如下所示:

*   `THRESHOLD`
*   `GRAY`
*   `OPAQUE`
*   `INVERT`
*   `POSTERIZE`
*   `BLUR`
*   `ERODE`
*   `DILATE`

除了这些预设，您还可以实现自己的预设。以下练习将说明如何基于现有图像在画布上进行绘制。你要测试的第一个预设是 Photoshop 中的马赛克效果。马赛克效果实质上是在保持图像尺寸的同时降低图像分辨率。让我们来看看这个练习的代码，`Chapter03_17`:

```py
PImage img;
int step;

void setup() {
  size(1500, 750);
  background(0);
  img = loadImage("landscape.png");
  img.loadPixels();
  step = 10;
  noStroke();
  noLoop();
}

void draw() {
  for (int y=0; y<img.height; y+=step) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x+=step) {
      color col = img.pixels[rows+x];
      fill(col);
      rect(x+img.width, y, step, step);
    }
  }
  image(img, 0, 0);
}

```

注意，在嵌套的`for`循环中，你不需要遍历每一个像素。相反，您用变量`step`中的一个值来增加索引。然后对这些像素的颜色进行采样，并将其用作正方形的`fill()`颜色。图 [3-22](#Fig22) 显示了原始照片和拼接图像。

![A436326_1_En_3_Fig22_HTML.jpg](A436326_1_En_3_Fig22_HTML.jpg)

图 3-22。

Mosaic effect example

如果将`rect()`命令替换为`ellipse()`命令，可以实现圆形镶嵌效果，如图 [3-23](#Fig23) 所示。

![A436326_1_En_3_Fig23_HTML.jpg](A436326_1_En_3_Fig23_HTML.jpg)

图 3-23。

Mosaic effect with circles

前两个练习对矩形和圆形使用`fill()`颜色。如果你使用`stroke()`颜色来绘制线条，你可以对同一张照片进行不同的渲染，类似于图 [3-24](#Fig24) 。

![A436326_1_En_3_Fig24_HTML.jpg](A436326_1_En_3_Fig24_HTML.jpg)

图 3-24。

Mosaic effect with short line segments

在`draw()`函数中，您使用随机机制`floor(random(2))`来选择线段的绘制方向。其结果将是 0 或 1。您可以用它来确定对角线线段的方向。

```py
PImage img;
int step;

void setup() {
  size(1500, 750);
  background(0);
  img = loadImage("landscape.png");
  img.loadPixels();
  step = 10;
  smooth();
  noFill();
  noLoop();
}

void draw() {
  for (int y=0; y<img.height; y+=step) {
    int rows = y*img.width;
    for (int x=0; x<img.width; x+=step) {
      color col = img.pixels[rows+x];
      stroke(col);
      int num = floor(random(2));
      if (num == 0) {
        line(x+img.width, y, x+img.width+step, y+step);
      } else {
        line(x+img.width+step, y, x+img.width, y+step);
      }
    }
  }
  image(img, 0, 0);
}

```

下一个练习`Chapter03_20`，探索图像处理中常见的条形码效应。Irma Boom 等著名设计师也对旧的经典画作进行了采样，并用垂直色条来表示，类似于您在本练习中计划做的事情。首先，你拍摄一张彩色照片，并在照片中间添加一条水平线(图 [3-25](#Fig25) )。

![A436326_1_En_3_Fig25_HTML.jpg](A436326_1_En_3_Fig25_HTML.jpg)

图 3-25。

Sample photograph with a horizontal line

沿着水平线，对线上每个像素进行采样，并检索其颜色值。通过使用颜色值，您可以沿着水平线为每个像素绘制一条垂直线。代码如下:

```py
PImage img;

void setup() {
  size(1200, 900);
  background(0);
  img = loadImage("christmas.png");
  img.loadPixels();
  noFill();
  noLoop();
}

void draw() {
  int y = img.height/2;
  for (int x=0; x<img.width; x++) {
    color c = img.pixels[y*img.width+x];
    stroke(c);
    line(x, 0, x, img.height-1);
  }
}

```

这个程序很简单。视觉结果是原始照片的条形码表示，如图 [3-26](#Fig26) 所示。

![A436326_1_En_3_Fig26_HTML.jpg](A436326_1_En_3_Fig26_HTML.jpg)

图 3-26。

Barcode effect example

您可以通过将变量`y`改为`mouseY`并删除`noLoop()`函数来试验这个程序的交互版本。在这种情况下，结果是仅由一张照片生成一个美丽的动画。

到目前为止，您已经探索了基于现有图像创建新图像的各种方法，或者通过替换像素颜色，或者通过在画布上参照像素颜色进行绘制。在下一节中，您将学习如何组合两幅图像。

## 混合多个图像

处理有一个`blend()`功能( [`https://processing.org/reference/blend_.html`](https://processing.org/reference/blend_.html) )，它提供了许多选项来组合两幅图像。工作机制类似于 Photoshop 中的图层选项。本节将不详细解释每个选项。本节中的练习将说明组合两个图像的基本逻辑。下面的练习`Chapter03_21`演示了`blend()`功能在选项`ADD`处理中的使用:

```py
PImage img1, img2;

void setup() {
  size(1200, 900);
  background(0);
  img1 = loadImage("hongkong.png");
  img2 = loadImage("sydney.png");
  noLoop();
}

void draw() {
  img1.blend(img2, 0, 0, img2.width, img2.height,
    0, 0, img1.width, img1.height, ADD);
  image(img1, 0, 0);
}

```

您有两个`PImage`实例，`img1`和`img2`，每个实例都从`data`文件夹加载了一个外部图像。在`draw()`函数中，`img2`实例将通过`img1.blend()`方法融合到`img1`实例中。其余的参数是源偏移(`x, y`)和尺寸(`width, height`)、目标偏移(`x, y`)和尺寸(`width, height`)以及混合选项`ADD`。注意，在`blend()`功能之后，`img1`的内容将会改变。练习中使用的两幅图像大小相同(1200×900 像素)。然而，这里的`blend()`功能将改变`img2`的分辨率，如果两幅图像的尺寸不同。图 [3-27](#Fig27) 显示了结果图像。

![A436326_1_En_3_Fig27_HTML.jpg](A436326_1_En_3_Fig27_HTML.jpg)

图 3-27。

Blending two images with the ADD option

您也可以使用自己的代码在处理过程中执行这种混合效果。对于`ADD`选项，您可以将两幅图像中的两个像素颜色分量相加。因为 RGB 的有效范围是 0 到 255，所以您可以将值限制在此范围内。这里是练习的来源，`Chapter03_22`。在这个版本中，假设两个图像具有相同的大小(1200×900 像素)。

```py
PImage img1, img2, img3;

void setup() {
  size(1200, 900);
  background(0);
  img1 = loadImage("hongkong.png");
  img2 = loadImage("sydney.png");
  img3 = createImage(img1.width, img1.height, ARGB);
  noLoop();
}

void draw() {
  for (int i=0; i<img1.pixels.length; i++) {
    color c1 = img1.pixels[i];
    color c2 = img2.pixels[i];
    float r = constrain(red(c1) + red(c2), 0, 255);
    float g = constrain(green(c1) + green(c2), 0, 255);
    float b = constrain(blue(c1) + blue(c2), 0, 255);
    img3.pixels[i] = color(r, g, b);
  }
  img3.updatePixels();
  image(img3, 0, 0);
}

```

逻辑很简单。`draw()`函数有一个`for`循环来遍历`img1`和`img2`中的所有像素。三种颜色分量相加在一起，并限制在 0 到 255 的范围内。第三个`PImage`实例`img3`，存储所有新的像素颜色值，并在屏幕上显示图像。

作为演示，本节的最后一个练习`Chapter03_23`，也将展示一个在 OpenCV 中完成的版本。要在处理环境中使用 OpenCV，记得将`code`文件夹复制到你的 sketch 文件夹中，并在一个新的选项卡中重新创建`CVImage`类，如前一章所示。您将创建三个`CVImage`类的实例来维护`hongkong.png`、`sydney.png`和生成的图像。这里显示了主程序的示例代码。同样，假设两个源图像具有相同的大小(1200×900 像素)。

```py
CVImage img1, img2, img3;

void setup() {
  size(1200, 900);
  System.loadLibrary(Core.NATIVE_LIBRARY_NAME);
  background(0);
  PImage tmp = loadImage("hongkong.png");
  img1 = new CVImage(tmp.width, tmp.height);
  img2 = new CVImage(tmp.width, tmp.height);
  img3 = new CVImage(tmp.width, tmp.height);
  img1.copyTo(tmp);
  tmp = loadImage("sydney.png");
  img2.copyTo(tmp);
  noLoop();
}

void draw() {
  Mat m1 = img1.getBGR();
  Mat m2 = img2.getBGR();
  Mat m3 = new Mat(m1.size(), m1.type());
  Core.add(m1, m2, m3);
  img3.copyTo(m3);
  image(img3, 0, 0);
  m1.release();
  m2.release();
  m3.release();
}

```

程序的主要命令是`Core.add()`功能。它将前两个源矩阵与第三个源矩阵相加作为目标矩阵。它也依赖于你在前一章开发的`CVImage`类。图像对象使用`copyTo()`和`getBGR()`方法在处理格式和 OpenCV 格式之间转换。

## 结论

本章通过改变单个像素的颜色来介绍图像处理的基本任务。您现在了解了如何实现简单的图像滤镜，如灰度和反转滤镜。您还了解了如何从头开始创建图形图像，以及如何修改现有图像以进行创造性输出。在下一章中，你将改变像素的位置，这样你可以获得更动态的图像处理效果。